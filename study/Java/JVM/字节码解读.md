# Table of Contents

* [字节码解读](#字节码解读)
  * [简介](#简介)
  * [Demo代码](#demo代码)
  * [解析字节码文件](#解析字节码文件)
  * [解读字节码](#解读字节码)
    * [类相关信息和版本号信息](#类相关信息和版本号信息)
    * [静态常量池](#静态常量池)
    * [方法信息](#方法信息)
  * [指令大全](#指令大全)
    * [对象初始化指令](#对象初始化指令)
    * [栈内存操作指令](#栈内存操作指令)
    * [流程控制指令](#流程控制指令)
    * [算数运算指令](#算数运算指令)
    * [类型转换指令](#类型转换指令)
    * [方法调用指令](#方法调用指令)


# 字节码解读

## 简介

Java中的字节码是Java代码编译后生成的中间代码格式，JVM就是读取的字节码文件然后进行各种操作的。

简单来说，由.java文件编译生成的.class文件里的内容就是字节码。

如其名，Java字节码就是一组byte的指令集合，也就是说最多支持256个操作码，当然发展至今也才用了200左右，所以容量还是相当够的。

## Demo代码

```
public class Hello {

    public static void main(String[] args) {
        int a = 1;
        int b = 1;

        int sum = 0;
        for (int i = 0; i < 100; i++) {
            if (sum > 200) {
                break;
            }
            sum += a * b;
            a++;
            b = b + 2;
        }
    }
}
```

## 解析字节码文件

正常来说，字节码内容是没法看的（一串byte组成的内容，谁能直接看懂，我直呼牛逼），但每个字节都代表一个操作码（指令），也就是解析出人能看懂的指令，然后再加优化排版就能分析了，这就是java自带的反编译工具（类似gui以及idea自带反编译工具等等，那就是对反编译的结果再加以排版，就感觉像个java文件了）。

```
# 编译 java->class
javac Hello.java

# 反编译 不可读的class->可读的字节码
javap -c Hello.class
# 附加信息更多的反编译 推荐 接下来的分析内容以该操作返回为主
javap -c -verbose Hello.class
```


## 解读字节码

字节码内容
```
Classfile /Users/zhuweijie/personalProjects/linzone/target/classes/com/xyz/linzone/study/jike/Hello.class
  Last modified 2021年7月8日; size 634 bytes
  MD5 checksum 467ccdf86514aec9a4ddb32ccaa9ba17
  Compiled from "Hello.java"
public class com.xyz.linzone.study.jike.Hello
  minor version: 0
  major version: 52
  flags: (0x0021) ACC_PUBLIC, ACC_SUPER
  this_class: #2                          // com/xyz/linzone/study/jike/Hello
  super_class: #3                         // java/lang/Object
  interfaces: 0, fields: 0, methods: 2, attributes: 1
Constant pool:
   #1 = Methodref          #3.#25         // java/lang/Object."<init>":()V
   #2 = Class              #26            // com/xyz/linzone/study/jike/Hello
   #3 = Class              #27            // java/lang/Object
   #4 = Utf8               <init>
   #5 = Utf8               ()V
   #6 = Utf8               Code
   #7 = Utf8               LineNumberTable
   #8 = Utf8               LocalVariableTable
   #9 = Utf8               this
  #10 = Utf8               Lcom/xyz/linzone/study/jike/Hello;
  #11 = Utf8               main
  #12 = Utf8               ([Ljava/lang/String;)V
  #13 = Utf8               i
  #14 = Utf8               I
  #15 = Utf8               args
  #16 = Utf8               [Ljava/lang/String;
  #17 = Utf8               a
  #18 = Utf8               b
  #19 = Utf8               sum
  #20 = Utf8               StackMapTable
  #21 = Class              #16            // "[Ljava/lang/String;"
  #22 = Utf8               MethodParameters
  #23 = Utf8               SourceFile
  #24 = Utf8               Hello.java
  #25 = NameAndType        #4:#5          // "<init>":()V
  #26 = Utf8               com/xyz/linzone/study/jike/Hello
  #27 = Utf8               java/lang/Object
{
  public com.xyz.linzone.study.jike.Hello();
    descriptor: ()V
    flags: (0x0001) ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 7: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/xyz/linzone/study/jike/Hello;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: (0x0009) ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=5, args_size=1
         0: iconst_1
         1: istore_1
         2: iconst_1
         3: istore_2
         4: iconst_0
         5: istore_3
         6: iconst_0
         7: istore        4
         9: iload         4
        11: bipush        100
        13: if_icmpge     45
        16: iload_3
        17: sipush        200
        20: if_icmple     26
        23: goto          45
        26: iload_3
        27: iload_1
        28: iload_2
        29: imul
        30: iadd
        31: istore_3
        32: iinc          1, 1
        35: iload_2
        36: iconst_2
        37: iadd
        38: istore_2
        39: iinc          4, 1
        42: goto          9
        45: return
      LineNumberTable:
        line 10: 0
        line 11: 2
        line 13: 4
        line 14: 6
        line 15: 16
        line 16: 23
        line 18: 26
        line 19: 32
        line 20: 35
        line 14: 39
        line 22: 45
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            9      36     4     i   I
            0      46     0  args   [Ljava/lang/String;
            2      44     1     a   I
            4      42     2     b   I
            6      40     3   sum   I
      StackMapTable: number_of_entries = 3
        frame_type = 255 /* full_frame */
          offset_delta = 9
          locals = [ class "[Ljava/lang/String;", int, int, int, int ]
          stack = []
        frame_type = 16 /* same */
        frame_type = 250 /* chop */
          offset_delta = 18
    MethodParameters:
      Name                           Flags
      args
}
SourceFile: "Hello.java"
```

初次相见，那肯定是一脸懵逼，毕竟这玩意和我们写的代码差距也太大了，这里我们拆开来慢慢分析。


### 类相关信息和版本号信息

```
public class com.xyz.linzone.study.jike.Hello
  minor version: 0 //jdk小版本信息
  major version: 52 //jdk大版本信息
  flags: (0x0021) ACC_PUBLIC, ACC_SUPER
  this_class: #2                          // 指向常量池#2 就是本类 com/xyz/linzone/study/jike/Hello
  super_class: #3                         // 超类 java/lang/Object
  interfaces: 0, fields: 0, methods: 2, attributes: 1 //实现了0个接口 有0个成员变量、2个方法、1个属性
```



### 静态常量池

```
Constant pool:
   #1 = Methodref          #3.#25         // java/lang/Object."<init>":()V
   #2 = Class              #26            // com/xyz/linzone/study/jike/Hello
   #3 = Class              #27            // java/lang/Object
   #4 = Utf8               <init>
   #5 = Utf8               ()V
   #6 = Utf8               Code
   #7 = Utf8               LineNumberTable
   #8 = Utf8               LocalVariableTable
   #9 = Utf8               this
  #10 = Utf8               Lcom/xyz/linzone/study/jike/Hello;
  #11 = Utf8               main
  #12 = Utf8               ([Ljava/lang/String;)V
  #13 = Utf8               i
  #14 = Utf8               I
  #15 = Utf8               args
  #16 = Utf8               [Ljava/lang/String;
  #17 = Utf8               a
  #18 = Utf8               b
  #19 = Utf8               sum
  #20 = Utf8               StackMapTable
  #21 = Class              #16            // "[Ljava/lang/String;"
  #22 = Utf8               MethodParameters
  #23 = Utf8               SourceFile
  #24 = Utf8               Hello.java
  #25 = NameAndType        #4:#5          // "<init>":()V
  #26 = Utf8               com/xyz/linzone/study/jike/Hello
  #27 = Utf8               java/lang/Object
```

说几个重点的看法吧

- #n 代表常量编号，其他地方引用常量就是根据这个编号
- Methodref 代表这个常量指向一个方法
- = 就是分隔符
- NameAndType 方法名:方法类型

### 方法信息

两个方法，一个是我们的main函数，还有个就是默认的空构造函数。

直接分析我们的main函数
```
  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: (0x0009) ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=5, args_size=1
         0: iconst_1            // 将1压栈
         1: istore_1            // 栈顶出栈，放入本地变量表下标1处（不要奇怪为啥不从0开始，想想入参在哪），两步完成a=1
         2: iconst_1            // 将1压栈
         3: istore_2            // 栈顶出栈，放入本地变量表下标2处，两步完成b=1
         4: iconst_0            // 将0压栈
         5: istore_3            // 栈顶出栈，放入本地变量表下标3处，两步完成sum=0
         6: iconst_0            // 将0压栈
         7: istore        4     // 栈顶出栈，放入本地变量表下标4处，两步完成for循环 i=0
         9: iload         4     // 加载本地变量表下标4值入栈，i值
        11: bipush        100   // 将100压栈
        13: if_icmpge     45    // 出栈上面两个入栈值 比较i>=100 若true 结束for循环 前往45 即return 否则继续
        16: iload_3             // 加载本地变量表下标3值入栈，sum值
        17: sipush        200   // 将200压栈
        20: if_icmple     26    // 出栈上面两个入栈值 比较sum<=200 若true 前往26继续 否则下一步
        23: goto          45    // 前往45 即return
        26: iload_3             // 加载本地变量表下标3值入栈，sum值
        27: iload_1             // 加载本地变量表下标2值入栈，a值
        28: iload_2             // 加载本地变量表下标2值入栈，b值（连续压栈3次，就是栈大小3的来源）
        29: imul                // 出栈栈顶俩数据 做乘法运算 结果压栈 即a*b
        30: iadd                // 出栈栈顶俩数据 做加法运算 结果压栈 即sum + a * b
        31: istore_3            // 出栈栈顶值放入本地变量表下标3处 即运算结果更新sum
        32: iinc          1, 1  // 本地变量表下标1值自增1，即a++
        35: iload_2             // 加载本地变量表下标2值入栈，b值
        36: iconst_2            // 将2压栈
        37: iadd                // 出栈栈顶俩数据 做加法运算 结果压栈 即b+2
        38: istore_2            // 出栈栈顶值放入本地变量表下标2处 即运算结果更新b
        39: iinc          4, 1  // 本地变量表下标4值自增1，即i++
        42: goto          9     // 前往9，for循环继续
        45: return              // 结束
      LineNumberTable:
        line 10: 0
        line 11: 2
        line 13: 4
        line 14: 6
        line 15: 16
        line 16: 23
        line 18: 26
        line 19: 32
        line 20: 35
        line 14: 39
        line 22: 45
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            9      36     4     i   I
            0      46     0  args   [Ljava/lang/String;
            2      44     1     a   I
            4      42     2     b   I
            6      40     3   sum   I
      StackMapTable: number_of_entries = 3
        frame_type = 255 /* full_frame */
          offset_delta = 9
          locals = [ class "[Ljava/lang/String;", int, int, int, int ]
          stack = []
        frame_type = 16 /* same */
        frame_type = 250 /* chop */
          offset_delta = 18
    MethodParameters:
      Name                           Flags
      args
```

> descriptor: ([Ljava/lang/String;)V

- 代表方法的描述信息
- 左边小括号内是入参信息
    - `[` 代表参数是数组
    - `java/lang/String` 代表参数的类型
- 右边V表示这个方法的返回值是void

> flags: (0x0009) ACC_PUBLIC, ACC_STATIC

- 代表类的标志
- `ACC_PUBLIC` 代表public
- `ACC_STATIC` 代表static

> Code:

- 包围的就是关于代码的了

> stack=3, locals=5, args_size=1

- `stack=3` 代表栈深度3
- `locals=5` 代表本地变量表大小5
- `args_size=1` 代表参数数量1



## 指令大全

### 对象初始化指令

- `new` 简单明了


### 栈内存操作指令

- `dup` 复制栈顶值，并加复制的结果压栈
- `pop` 删除栈顶值
- `swap` 交换栈顶两个元素的值
- `dup_x1` 复制栈顶值，放到复制值下面一个值的下面，即第三位
    - 解读：dup复制栈顶值，x1推移至复制值后1位的后面
    - x1还可以是x2，数字几就是几位
    - 数字几代表几个32位值
- `dup2_x1` 复制栈顶1个64位或2个32位值，放入复制的值下面一个32位值的下面
    - 解读：可以同dup_x1，dup2代表复制栈顶俩元素（1个64位或2个32位，后面不多加解释），依次类推
- `astore_N`或`astore N` 栈顶元素出栈，赋值给局部变量表N下标
- `aload_N`或`aload N` 局部变量表N下标值压栈

### 流程控制指令

- `if_icmpge N` 翻译就是`if int compare great equal`，两数比较，一个大于等于另一个，程序跳转至N
- ...


### 算数运算指令

| | + | - | * | / | % | -() |
| --- | --- | --- | --- | --- | --- | --- | 
| int | iadd | isub | imul | idiv | irem | ineg | 
| long | ladd | lsub | imul | ldiv | lrem | lneg | 
| float | fadd | fsub | fmul | fdiv | frem | fneg | 
| double | dadd | dsub | dmul | ddiv | drem | dneg | 


### 类型转换指令

例如int值作为参数传递给实际接收long的方法时，会遇到类型转换

| | int | long | float | double | byte | char | short |
| --- | --- | --- | --- | --- | --- | --- | --- |
| int | --- | i2l | i2f | i2d | i2b | i2c | i2s |
| long | l2i | --- | l2f | l2d | --- | --- | --- |
| float | f2i | f2l | --- | f2d | --- | --- | --- |
| double | d2i | d2l | d2f | --- | --- | --- | --- |


### 方法调用指令


- `invokestatic` 调用静态方法
- `invokespecial` 调用构造函数、private方法、可见的超类方法
- `invokevirual` 调用公共方法、受保护和打包的私有方法
- `invokeinterface` 调用接口方法
